plugins {
    id 'org.springframework.boot' version '2.4.5' apply false
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.google.protobuf' version '0.8.15'
    id 'java'
    id 'distribution'
}

ext['grpc.version'] = '1.37.0'
ext['grpc.protoc.version'] = '3.15.8'

allprojects {
	group = 'com.sitewhere'
	version = '3.1.0'
	
	repositories {
        mavenLocal()
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'eclipse'

	apply plugin: "com.github.hierynomus.license"
	license {
		header rootProject.file('HEADER')
		include "**/*.java"
	}

 	// Choose Java settings.
	apply plugin: 'java-library'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	// Common dependencies used for all projects.
	dependencies {
        // Spring dependency managment.
        implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

		// SiteWhere Java model.
		api group: 'com.sitewhere', name: 'sitewhere-java-model', version: '3.1.0'

		implementation group: 'org.apache.commons', name: 'commons-lang3'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'

		testCompile group: 'junit', name: 'junit', version: '4.12'
	}
}
